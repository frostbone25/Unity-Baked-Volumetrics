#define THREAD_GROUP_SIZE_X 8
#define THREAD_GROUP_SIZE_Y 8
#define THREAD_GROUP_SIZE_Z 8

//https://github.com/TwoTailsGames/Unity-Built-in-Shaders/blob/master/CGIncludes/UnityCG.cginc
//From UnityCG.cginc
#define UNITY_PI            3.14159265359f
#define UNITY_TWO_PI        6.28318530718f

//This acts like a bias for the surface tracing functions, 1 gives best results.
//Though this can cause issues with thin geometry (i.e. things that are only represented as a single voxel and no neighboring ones)
//TODO: With voxelization, introduce an adjustable thickness modifier when generating them for the scene.
#define SURFACE_ENVIRONMENT_OCCLUSION_SKIP_ITERATION 0

//[FIX]: (Thanks Pema!) This is a solution to solve the problem with causing TDR/driver timeouts.
//We force the occlusion checking loop to terminate at some point even if it manages to run forever somehow.
#define MAX_LOOP_ITERATIONS 2048

#include "ComputeTextureSampling.cginc"
#include "Random.cginc"
#include "Sampling.cginc"
#include "VoxelTracerUtilities.cginc"

//|||||||||||||||||||||||||||||||||||||| KEYWORDS ||||||||||||||||||||||||||||||||||||||
//|||||||||||||||||||||||||||||||||||||| KEYWORDS ||||||||||||||||||||||||||||||||||||||
//|||||||||||||||||||||||||||||||||||||| KEYWORDS ||||||||||||||||||||||||||||||||||||||

#pragma multi_compile_local _ NORMAL_ORIENTED_HEMISPHERE_SAMPLING

//|||||||||||||||||||||||||||||||||||||| VARIABLES ||||||||||||||||||||||||||||||||||||||
//|||||||||||||||||||||||||||||||||||||| VARIABLES ||||||||||||||||||||||||||||||||||||||
//|||||||||||||||||||||||||||||||||||||| VARIABLES ||||||||||||||||||||||||||||||||||||||

TextureCube<float4> EnvironmentMap;

Texture3D<float4> SceneAlbedo;
Texture3D<float4> SceneNormal;

RWTexture3D<float4> Write;

float3 VolumePosition;
float3 VolumeSize;
float3 VolumeResolution;

int MaxEnvironmentSamples;

float AlbedoBoost;
float EnvironmentIntensity;

RWStructuredBuffer<int> DummyComputeBuffer;

//||||||||||||||||||||||||||||| ComputeShader_TraceSurfaceEnvironmentLight |||||||||||||||||||||||||||||
//||||||||||||||||||||||||||||| ComputeShader_TraceSurfaceEnvironmentLight |||||||||||||||||||||||||||||
//||||||||||||||||||||||||||||| ComputeShader_TraceSurfaceEnvironmentLight |||||||||||||||||||||||||||||

#pragma kernel ComputeShader_TraceSurfaceEnvironmentLight
[numthreads(THREAD_GROUP_SIZE_X, THREAD_GROUP_SIZE_Y, THREAD_GROUP_SIZE_Z)]
void ComputeShader_TraceSurfaceEnvironmentLight(uint3 id : SV_DispatchThreadID)
{
    float3 voxelPosition = id.xyz + float3(0.5, 0.5, 0.5);
    voxelPosition /= VolumeResolution.xyz;

    float3 singleVoxelSize = VolumeSize / VolumeResolution;
    float3 volumeHalfSize = VolumeSize * 0.5f;
    float3 worldVoxelPosition = VolumePosition + (voxelPosition * VolumeSize) - volumeHalfSize;

    //float4 sceneAlbedo = TEX3D_SHARP(SceneAlbedo, voxelPosition);
    float4 sceneAlbedo = SceneAlbedo[id.xyz];

    //[OPTIMIZATION]
    //Since we are tracing "surface light", we don't care about transparent voxels, only opaque ones matter.
    //So if the current voxel is transparent, then we stop here.
    if (!(sceneAlbedo.a > 0.0)) 
    {
        Write[id.xyz] = float4(0, 0, 0, 0);
        return;
    }

    //float4 sceneNormal = TEX3D_SHARP(SceneNormal, voxelPosition);
    float4 sceneNormal = SceneNormal[id.xyz];
    sceneNormal = sceneNormal * 2.0f - 1.0f;

    //trace lights
    float4 finalColor = float4(0, 0, 0, 0);

    float2 noise = float2(GenerateRandomFloat(voxelPosition.xyz * VolumeResolution), GenerateRandomFloat(voxelPosition.xyz * VolumeResolution));

    float3 vector_environmentRayPosition = worldVoxelPosition;

    #if defined (NORMAL_ORIENTED_HEMISPHERE_SAMPLING)
        float3 vector_environmentRayDirection = SampleHemisphereCosine(noise.x, noise.y, sceneNormal);
    #else
        float3 vector_environmentRayDirection = SampleSphereUniform(noise.x, noise.y);
    #endif

    int iterationIndex = 0;

    //NOTE: This is the most expensive operation, marching our ray until we hit an opaque voxel.
    while(iterationIndex < MAX_LOOP_ITERATIONS)
    {
        //if (PositionInVolumeBounds(vector_environmentRayPosition.xyz, VolumePosition, VolumeSize) == false)
            //break;

        //keep stepping the ray in world space
        vector_environmentRayPosition += vector_environmentRayDirection * singleVoxelSize;

        if (iterationIndex >= SURFACE_ENVIRONMENT_OCCLUSION_SKIP_ITERATION)
        {
            float3 environmentLight_scaledRayPosition = ((vector_environmentRayPosition + volumeHalfSize) - VolumePosition) / VolumeSize;
            float4 occlusionSample = TEX3D_SHARP(SceneAlbedo, environmentLight_scaledRayPosition);

            if (occlusionSample.a > 0.0)
                break;
            
            if (PositionInVolumeBounds(vector_environmentRayPosition.xyz, VolumePosition, VolumeSize) == false)
            {
                float3 environmentLightSample = TEXCUBE_SHARP(EnvironmentMap, vector_environmentRayDirection).rgb * EnvironmentIntensity;
                float localNdotL = max(0.0, dot(sceneNormal, vector_environmentRayDirection));
                environmentLightSample *= localNdotL;
                //environmentLightSample *= sceneAlbedo.rgb * AlbedoBoost;
                environmentLightSample *= (sceneAlbedo.rgb / UNITY_PI) * AlbedoBoost; //energy conserving lambertian BDRF

                finalColor.rgb += environmentLightSample;

                break;
            }
        }

        iterationIndex++;
    }

    finalColor = max(0.0f, finalColor);

    //Write[id.xyz] += finalColor * (1.0f / MaxEnvironmentSamples);
    Write[id.xyz] = float4(Write[id.xyz].rgb + finalColor.rgb * (1.0f / MaxEnvironmentSamples), 1);
}